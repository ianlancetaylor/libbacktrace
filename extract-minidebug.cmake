if (V2)
    set(SUFFIX_DSYMS ".dsyms2")
    set(SUFFIX_FSYMS ".fsyms2")
    set(SUFFIX_KEEPSYMS ".keepsyms2")
    set(SUFFIX_DBG ".dbg2")
    set(SUFFIX_MDBG ".mdbg2")
else()
    set(SUFFIX_DSYMS ".dsyms")
    set(SUFFIX_FSYMS ".fsyms")
    set(SUFFIX_KEEPSYMS ".keepsyms")
    set(SUFFIX_DBG ".dbg")
    set(SUFFIX_MDBG ".mdbg")
endif()

get_filename_component(SRC_DIR ${SRC} DIRECTORY)
get_filename_component(SRC_STEM ${SRC} NAME_WE)
get_filename_component(DST_DIR ${DST} DIRECTORY)
get_filename_component(DST_STEM ${DST} NAME_WE)

set(DST_DSYMS ${DST_DIR}/${DST_STEM}${SUFFIX_DSYMS})
set(DST_FSYMS ${DST_DIR}/${DST_STEM}${SUFFIX_FSYMS})
set(DST_KEEPSYMS ${DST_DIR}/${DST_STEM}${SUFFIX_KEEPSYMS})
set(DST_DBG ${DST_DIR}/${DST_STEM}${SUFFIX_DBG})
set(DST_MDBG ${DST_DIR}/${DST_STEM}${SUFFIX_MDBG})


execute_process(COMMAND ${NM} -D ${SRC} -P --defined-only OUTPUT_VARIABLE NM_RESULT)
string(REPLACE "\n" ";" DSYM_TABLE ${NM_RESULT})
foreach(DSYM_INFO ${DSYM_TABLE})
    string(REGEX MATCH "^([^ \n]+) +([^ \n]+)" MATCH ${DSYM_INFO})
    list(APPEND DSYM_LIST ${CMAKE_MATCH_1})
endforeach()
list(SORT DSYM_LIST)
foreach(DSYM_NAME ${DSYM_LIST})
    file(APPEND ${DST_DSYMS} "${DSYM_NAME}\n")
endforeach()

execute_process(COMMAND ${NM} ${SRC} -P --defined-only OUTPUT_VARIABLE NM_RESULT)
string(REPLACE "\n" ";" FSYM_TABLE ${NM_RESULT})
foreach(FSYM_INFO ${FSYM_TABLE})
    string(REGEX MATCH "^([^ \n]+) +([^ \n]+)" MATCH ${FSYM_INFO})
    if (${CMAKE_MATCH_2} STREQUAL "T" OR ${CMAKE_MATCH_2} STREQUAL "t" OR ${CMAKE_MATCH_2} STREQUAL "D")
        list(APPEND FSYM_LIST ${CMAKE_MATCH_1})
    endif()
endforeach()
list(SORT FSYM_LIST)
foreach(FSYM_NAME ${FSYM_LIST})
    file(APPEND ${DST_FSYMS} "${FSYM_NAME}\n")
endforeach()

execute_process(COMMAND ${COMM} -13 ${DST_DSYMS} ${DST_FSYMS} OUTPUT_FILE ${DST_KEEPSYMS})
execute_process(COMMAND ${OBJCOPY} --only-keep-debug ${SRC} ${DST_DBG})
execute_process(COMMAND ${OBJCOPY} -S --remove-section .gdb_index --remove-section .comment --keep-symbols=${DST_KEEPSYMS} ${DST_DBG} ${DST_MDBG})
execute_process(COMMAND ${OBJCOPY} --strip-all --remove-section ..comment ${SRC} ${DST})
file(REMOVE ${DST_MDBG}.xz)
execute_process(COMMAND ${XZ} ${DST_MDBG})
if (V2)
    execute_process(COMMAND ${READELF} -n ${SRC} OUTPUT_VARIABLE READELF_RESULT)
    string(REGEX MATCH "Build ID: *([a-z0-9]+)" MATCH ${READELF_RESULT})
    set(BUILD_ID ${CMAKE_MATCH_1})
    file(WRITE ${DST_DIR}/${DST_STEM}.build-id ${BUILD_ID})
    execute_process(COMMAND ${CMAKE_COMMAND} -E copy ${DST_DBG} ${DST_DIR}/${DST_STEM}.debug)
    execute_process(COMMAND ${OBJCOPY} --add-section .gnu_debugdata=${DST_MDBG}.xz ${DST_DBG})
endif()
execute_process(COMMAND ${OBJCOPY} --add-section .gnu_debugdata=${DST_MDBG}.xz ${DST})
